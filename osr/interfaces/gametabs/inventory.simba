(*
Inventory
=========
Methods to interact with the inventory.
*)

{$DEFINE SRL_INVENTORY_INCLUDED}
{$IFNDEF SRL_OSR}
  {$I SRL-T/osr.simba}
{$ENDIF}

const
  //Column wise and "reversed" column wise - normal pattern
  DROP_PATTERN_REGULAR: TIntegerArray = [0..27];
  DROP_PATTERN_REGULAR_R: TIntegerArray = [3,2,1,0, 7,6,5,4, 11,10,9,8, 15,14,13,12, 19,18,17,16, 23,22,21,20, 27,26,25,24];

  //Column wise snake and "reversed" version
  DROP_PATTERN_SNAKE: TIntegerArray  = [0,1,2,3, 7,6,5,4, 8,9,10,11, 15,14,13,12, 16,17,18,19, 23,22,21,20, 24,25,26,27];
  DROP_PATTERN_SNAKE_R: TIntegerArray = [3,2,1,0, 4,5,6,7, 11,10,9,8, 12,13,14,15, 19,18,17,16, 20,21,22,23, 27,26,25,24];

  //by row (and "reversed")
  DROP_PATTERN_TOPDOWN: TIntegerArray = [0,4,8,12,16,20,24,  1,5,9,13,17,21,25,  2,6,10,14,18,22,26, 3,7,11,15,19,23,27];
  DROP_PATTERN_TOPDOWN_R: TIntegerArray = [3,7,11,15,19,23,27, 2,6,10,14,18,22,26, 1,5,9,13,17,21,25,  0,4,8,12,16,20,24];

  //Spiral pattern: Starts in slot 0(top left corner of invy) and circles in a clockwise spiral
  DROP_PATTERN_SPIRAL: TIntegerArray   = [0,1,2,3, 7,11,15,19,23, 27,26,25,24, 20,16,12,8,4, 5, 6,10,14,18,22, 21,17,13,9];

type
  TRSInventory = record(TRSInterface)
    LOW_SLOT: Int32;
    HIGH_SLOT: Int32;
  end;


(*
Inventory.GetSlotBoxes
~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.GetSlotBoxes(): TBoxArray;

Mainly used internally by TRSInventory methods.
This function is used to return a TBoxArray of the inventory slots.

Example
-------

  Debug(Inventory.GetSlotBoxes());
*)
function TRSInventory.GetSlotBoxes(): TBoxArray;
begin
  Result := Grid(4, 7, 31, 31, [11, 5], [Self.X1 + 13, Self.Y1 + 9]);
end;

function TRSInventory.GetSlotBox(slot: Int32): TBox;
begin
  if InRange(slot, 0, HIGH_SLOT) then
    Result := Self.GetSlotBoxes()[slot];
end;

procedure TRSInventory.Setup(); override;
begin
  inherited;

  Self.Name := 'Inventory';

  Self.LOW_SLOT := 0;
  Self.HIGH_SLOT := 27;
  Self.ItemInterface.Name := Self.Name + '.ItemInterface';
  Self.ItemInterface.GetSearchBoxesFunction := @Self.GetSlotBoxes;
end;

procedure TRSInventory.SetupAlignment(Mode: ERSClientMode); override;
begin
  inherited;

  Self.Alignment.Left := [@GameTab.X1];
  Self.Alignment.Right := [@GameTab.X2];
  Self.Alignment.Top := [@GameTab.Y1];
  Self.Alignment.Bottom := [@GameTab.Y2];
end;

(*
Inventory.IsOpen
~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.IsOpen(): Boolean;

Returns true if the inventory gametab is open, false if it's not.

Example
-------

  WriteLn Inventory.IsOpen();
*)
function TRSInventory.IsOpen(): Boolean;
begin
  Result := (GameTabs.GetCurrentTab() = ERSGameTab.INVENTORY);
end;

(*
Inventory.Open
~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.Open(): Boolean;

Attempts to open the inventory, returns true if it succeeds.

Example
-------

  WriteLn Inventory.Open();
*)
function TRSInventory.Open(): Boolean;
begin
  Result := GameTabs.Open(ERSGameTab.INVENTORY);
end;


(*
Inventory.PointToSlot
~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.PointToSlot(P: TPoint): Int32;

Returns the inventory slot index which contains the TPoint `P`.
Returns -1 if the point does not fall in any slots.

Example
-------

  var P: TPoint;
  var Slot: Int32;

  P := Inventory.GetSlotBox(15).Middle; // Example point

  Slot := Inventory.PointToSlot(P);
  WriteLn(Slot); // 15
*)
function TRSInventory.PointToSlot(p: TPoint): Int32;
begin
  Result := Self.ItemInterface.PointToSlot(p);
end;

(*
Inventory.MouseSlot
~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.MouseSlot(slot: Int32): Boolean;

Moves the mouse over the slot. Slot is an Integer between 0 and 27.

Example
-------

  if Inventory.HoverSlot(1) then
    WriteLn('Mouse is now over slot #1');
*)
function TRSInventory.MouseSlot(slot: Int32): Boolean;
begin
  Result := Self.ItemInterface.MouseSlot(slot);
end;

function TRSInventory.HoverSlot(slot: Int32): Boolean; deprecated 'Use TRSInventory.MouseSlot() instead!';
begin
  Result := Self.MouseSlot(slot);
end;


(*
Inventory.ClickSlot
~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.ClickSlot(slot: Int32; option: String = ''): Boolean;

Moves the mouse and clicks on the slot. Slot is an Integer between 0 and 27.

If option is empty the slot is left clicked. Else a right click is performed
and the option is selected from the choose option menu.

Example
-------

  if Inventory.ClickSlot(1) then
    WriteLn('Left clicked slot #1');
    
  if Inventory.ClickSlot(1, 'Drop') then
    WriteLn('Right clicked and selected "Drop" on slot #1', 'Drop');  
*)
function TRSInventory.ClickSlot(slot: Int32; option: String = ''): Boolean;
begin
  Result := Self.ItemInterface.InteractSlot(slot, option);
end;

function TRSInventory.IsSlotUsed(b: TBox): Boolean; overload;
begin
  // black with tolerance for when item is transparent
  Result := SRL.CountColor(CTS1(1118740, 35), b) > 0;
end;

function TRSInventory.IsSlotUsed(slot: Int32): Boolean; overload;
begin
  Result := Self.Open() and Self.IsSlotUsed(Self.GetSlotBox(slot));
end;

(*
Inventory.IsSlotSelected
~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.IsSlotSelected(Slot: Int32): Boolean;

Returns True if the slot is selected (white outline).

Example
-------

  if Inventory.IsSlotSelected(1) then
    WriteLn('Slot 1 is selected!');
*)
function TRSInventory.IsSlotSelected(slot: Int32): Boolean;
var
  b: TBox;
begin
  if not Self.Open() then
    Exit(False);

  b := Self.GetSlotBox(slot);
  b.Y1 += 8; // Skip item stack text

  Result := SRL.CountColor($FFFFFF, b) > 0;
end;

(*
Inventory.GetSelectedSlot
~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.GetSelectedSlot: Int32;

Returns the index of the selected slot (white outline).
**-1** is returned if no slot is selected.

Example
-------

  WriteLn(Inventory.GetSelectedSlot());
*)
function TRSInventory.GetSelectedSlot(): Int32; //CAN BE OPTIMIZED WITH TPAs
var
  I: Int32;
begin
  if not Self.Open() then
    Exit(-1);

  for I := Self.LOW_SLOT to Self.HIGH_SLOT do
    if Self.IsSlotSelected(I) then
      Exit(I);

  Result := -1;
end;

(*
Inventory.SetSelectedSlot
~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.SetSelectedSlot(Slot: Int32): Boolean;

Set the slot as selected (white outline).
Slot can be **-1** to unselect the currently selected slot.

Example
-------

  WriteLn(Inventory.GetSelectedSlot());
*)
function TRSInventory.SetSelectedSlot(slot: Int32): Boolean;
var
  selected: Int32;
begin
  selected := Self.GetSelectedSlot();
  if (selected = Slot) then
    Exit(True);

  if (selected > -1) then
    Mouse.Click(Self.GetSlotBox(selected), MOUSE_LEFT);
  if (Slot > -1) then
    Mouse.Click(Self.GetSlotBox(slot), MOUSE_LEFT);

  Result := WaitUntil(Self.GetSelectedSlot() = slot, 50, SRL.TruncatedGauss(1000, 2000));
end;

(*
Inventory.Use
~~~~~~~~~~~~~
.. pascal:: function TRSInventory.Use(Slot, OtherSlot: Int32): Boolean;

Selects **Slot** and uses with **OtherSlot**.

Example
-------

  Inventory.Use(1, 5); // Use item in slot 1 on Item in slot 5
*)
function TRSInventory.Use(Slot, OtherSlot: Int32): Boolean;
var
  b: TBox;
begin
  b := Self.GetSlotBox(OtherSlot);
  if b = [] then
    Exit;

  Result := Self.SetSelectedSlot(Slot);
  Wait(0, 1000, wdLeft);
  Mouse.Click(b, MOUSE_LEFT);
end;

(*
Inventory.RandomPattern
~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.RandomPattern(): TIntegerArray;

Returns a random inventory pattern.

Example
-------

  Inventory.ShiftDrop(Inventory.RandomPattern());
*)
function TRSInventory.RandomPattern(): TIntegerArray;
var
  Patterns: T2DIntegerArray;
begin

  Patterns := [
    DROP_PATTERN_SNAKE, DROP_PATTERN_SPIRAL,
    DROP_PATTERN_REGULAR, DROP_PATTERN_TOPDOWN,
    DROP_PATTERN_SNAKE_R, DROP_PATTERN_TOPDOWN_R
  ];

  Result := Patterns[Random(0, High(Patterns))];
end;

(*
Inventory.ErrorPattern
~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.ErrorPattern(Pattern: TIntegerArray=DROP_PATTERN_REGULAR; ErrorChance:Int32=5): TIntegerArray;

Returns the passed in pattern slightly modified.

Example
-------

  Inventory.ShiftDrop(Iventory.ErrorPattern(Inventory.RandomPattern()));
*)
function TRSInventory.ErrorPattern(Pattern: TIntegerArray=DROP_PATTERN_REGULAR; ErrorChance:Int32=5): TIntegerArray;
var i,j,h,k,tmp: Int32;
begin
  Result := Copy(Pattern);
  h := High(Pattern);
  i := 1;
  while i < h do
  begin
    if Random(100) < ErrorChance then
    begin
      case Random(0,2) of
        0:begin
            tmp := Result[i];
            for j:=i+1 to h do Result[j-1] := Result[j];
            Result[h] := tmp;
          end;
        1:begin
            Swap(Result[i+1],Result[i]);
          end;
        2:begin
            tmp := Result[i];
            k := Random(i+1,h);
            for j:=i+1 to k do Result[j-1] := Result[j];
            Result[k] := tmp;
          end;
      end;
    end;
    Inc(i);
  end;
end;

(*
Inventory.ShiftDrop
~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.ShiftDrop(Slots: TIntegerArray): Boolean;

Shift drops the inventory items in the pattern specified.

Example
-------

  Inventory.ShiftDrop(DROP_PATTERN_REGULAR);
*)
function TRSInventory.ShiftDrop(slots: TIntegerArray): Boolean;
var
  boxes: TBoxArray;
  box: TBox;
  i, j, attempts: Int32;
  selected: Int32;
  circle: TCircle;
begin
  if (Length(slots) = 0) then
    Exit(True);

  IsKeyDown(VK_SHIFT); // Clear cache
  boxes := Self.GetSlotBoxes();

  try
    for attempts := 1 to 5 do
    begin
      i := 0;

      while (i < Length(slots)) do
      begin
        box := boxes[slots[i]];
        selected := Self.GetSelectedSlot();
        if (selected > -1) then
        begin
          Self.SetSelectedSlot(-1);
          Break;
        end;

        if Self.IsSlotUsed(box) then
        begin
          if not IsKeyDown(VK_SHIFT) then
          begin
            KeyDown(VK_SHIFT);
            Wait(95,170,wdLeft); //Wait for SHIFT to be registered before the click (important).
          end; 

          Mouse.Click(box, MOUSE_LEFT);

          // Spam click a little
          for j := 1 to SRL.TruncatedGauss(0, 3) do
          begin
            circle.X := Mouse.Position().X;
            circle.Y := Mouse.Position().Y;
            circle.Radius := 5;

            Mouse.Click(circle, MOUSE_LEFT, True);
          end;

          Wait(0, 250, wdLeft);
        end;

        Inc(i);
      end;

      if (i = Length(slots)) then
        Exit;
    end;
  finally
    if IsKeyDown(VK_SHIFT) then
      KeyUp(VK_SHIFT);
  end;

  Result := attempts <= 5;
end;


procedure TRSInventory.Draw(Bitmap: TMufasaBitmap); override;
begin
  if not Self.IsOpen() then
    Exit;

  inherited();

  Bitmap.DrawBoxes(Self.GetSlotBoxes(), $00FFFF);
end;

(*
Inventory.Count
~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.Count(): Int32;

Counts how many items are in the inventory right now.

Example
-------

  WriteLn Inventory.Count();
*)
function TRSInventory.Count(): Int32;
begin
  if not Self.Open() then
    Exit;
  Result := Self.ItemInterface.Count();
end;

(*
Inventory.IsFull
~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.IsFull(): Boolean;

Returns true if the inventory is full.

Example
-------

  WriteLn Inventory.IsFull();
*)
function TRSInventory.IsFull(): Boolean;
begin
  Result := Self.Count() >= 28;
end;

(*
Inventory.WaitCount
~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.WaitCount(Count: Int32; WaitTime: Int32; Interval: Int32 = -1): Boolean;

Wait **WaitTime** until the Inventory.Count() matches **Count**.

Example
-------

  Inventory.WaitCount(28, 5000); //Wait up to 5 seconds to have 28 items in the inventory.
*)
function TRSInventory.WaitCount(Count: Int32; WaitTime: Int32; Interval: Int32 = -1): Boolean;
begin
  if (Interval = -1) then
    Interval := SRL.TruncatedGauss(50, 1500);

  Result := WaitUntil(Self.Count() = Count, Interval, WaitTime);
end;

(*
Inventory.RandomSlotNearby
~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.RandomSlotNearby(Slot: Int32; Slots: TIntegerArray): Int32;

Randomly returns one of the **Slots** weighted towards **Slot** by distance.

Example
-------

  var Slot: Int32;

  Slot := Inventory.RandomSlotNearby(0, [0..27]);
  WriteLn('This slot is likely nearby slot 0');
  WriteLn(Slot);
*)
function TRSInventory.RandomSlotNearby(slot: Int32; slots: TIntegerArray): Int32;
begin
  Result := Self.ItemInterface.RandomSlotNearby(slot, slots);
end;


(*
Inventory.FindItems
~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.FindItems(Items: TRSItemArray; out Slots: TIntegerArray): Boolean;

Attempts to find the items passed in **Items**. The function returns true if any item is found and
the slots of the found items will be returned in **Slots**.

Example
-------
  var
    slots: TIntegerArray;
  begin
    if Inventory.FindItems(['Shark', 'Lobster'], slots) then
      WriteLn('The slots that have sharks and lobsters are: ', slots);
  end;
*)
function TRSInventory.FindItems(items: TRSItemArray; out slots: TIntegerArray): Boolean;
begin
  if not Self.Open() then
    Exit;
  slots := Self.ItemInterface.IndicesOf(items);
  Result := Length(slots) > 0;
end;

(*
Inventory.FindItem
~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.FindItem(Item: TRSItem; out Slots: TIntegerArray): Boolean;
.. pascal:: function TRSInventory.FindItem(item: TRSItem; out slot: Int32): Boolean; overload;

Attempts to find the item passed in **Item**. The function returns true if the item was found can be
used to return a single slot or all slots containing the item.

Example
-------
  var
    slots: TIntegerArray;
    slot: Int32;
  begin
    if Inventory.FindItem('Shark', slots) then
      WriteLn('The slots that have sharks are: ', slots);

    if Inventory.FindItem('Shark', slot) then
      WriteLn('The first slot that has a shark is: ', slot);
  end;
*)
function TRSInventory.FindItem(item: TRSItem; out slots: TIntegerArray): Boolean;
begin
  Result := Self.FindItems([item], slots);
end;

function TRSInventory.FindItem(item: TRSItem; out slot: Int32): Boolean; overload;
begin
  slot := -1;
  if not Self.Open() then
    Exit;
  slot := Self.ItemInterface.IndexOf([item]);
  Result := slot <> -1;
end;


(*
Inventory.ContainsAny
~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.ContainsAny(items: TRSItemArray): Boolean;

Returns true if the inventory contains any of the items passed in **items**.
It's the same as TRSInventory.FindItems() but without requiring a **Slots** parameter.

Example
-------

  WriteLn Inventory.ContainsAny(['Shark', 'Lobster']);
*)
function TRSInventory.ContainsAny(items: TRSItemArray): Boolean;
begin
  Result := Self.Open() and Self.ItemInterface.ContainsAny(items);
end;

(*
Inventory.ContainsItem
~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.ContainsItem(item: TRSItem): Boolean;

Returns true if the inventory contains the item passed in **item**.
Same as TRSInventory.FindItem() but without requiring **Slots** or **slot** parameter.

Example
-------

  WriteLn Inventory.ContainsItem('Shark');
*)
function TRSInventory.ContainsItem(item: TRSItem): Boolean;
begin
  Result := Self.Open() and Self.ItemInterface.Contains(item);
end;

(*
Inventory.ContainsAll
~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.ContainsAll(items: TRSItemArray): Boolean;

Returns true if the inventory contains all items passed in **items**.

Example
-------

  WriteLn Inventory.ContainsAll('Shark');
*)
function TRSInventory.ContainsAll(items: TRSItemArray): Boolean;
begin
  Result := Self.Open() and Self.ItemInterface.ContainsAll(items);
end;


(*
Inventory.CountItem
~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.CountItem(Item: TRSItem): Int32;

Returns the amount of **Item** in the inventory.
Keep in mind this does not count a stack number, but every single item that occupies an inventory slot.

Example
-------

  WriteLn Inventory.CountItem('Shark');
*)
function TRSInventory.CountItem(item: TRSItem): Int32;
begin
  if Self.Open() then
    Result := Self.ItemInterface.Count(item);
end;

(*
Inventory.CountItemStack
~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.CountItemStack(Item: TRSItem): Int32;

Returns the stack amount of **Item** in the inventory.

Example
-------

  WriteLn Inventory.CountItemStack('Bronze arrows'); //will print the stack amount
  WriteLn Inventory.CountItemStack('Shark'); //will print 0 because sharks are not stackable.
*)
function TRSInventory.CountItemStack(item: TRSItem): Int32;
begin
  if Self.Open() then
    Result := Self.ItemInterface.CountStack(item);
end;


(*
Inventory.MouseItem
~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.MouseItem(Item: TRSItem): Boolean;

Hover **Item** in the inventory.

Example
-------

  if Inventory.MouseItem('Bronze arrows') then
    ChooseOption.Select('Drop');
*)
function TRSInventory.MouseItem(item: TRSItem): Boolean;
begin
  if not Self.IsOpen() then
    Exit;
  Result := Self.ItemInterface.MouseItem(item);
end;

function TRSInventory.HoverItem(item: TRSItem): Boolean; deprecated 'Use TRSInventory.MouseItem() instead!';
begin
  Result := Self.MouseItem(item);
end;


(*
Inventory.ClickItem
~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.ClickItem(Item: TRSItem; Option: String = ''): Boolean;

Clicks **Item** in the inventory. If **Option** is specified that option will be
selected with right click if it exists.

Example
-------

  WriteLn Inventory.ClickItem('Attack potion(3)');
  WriteLn Inventory.ClickItem('Ashes', 'Drop');
*)
function TRSInventory.ClickItem(item: TRSItem; option: String = ''): Boolean;
begin
  if not Self.IsOpen() then
    Exit;

  Result := Self.ItemInterface.InteractItem(item, option);
end;

(*
Inventory.Use
~~~~~~~~~~~~~
.. pascal:: function TRSInventory.Use(Item, OtherItem: TRSItem): Boolean; overload;

Use one item on another

Example
-------

  WriteLn Inventory.Use('Knife', 'Oak logs');
*)
function TRSInventory.Use(Item, OtherItem: TRSItem): Boolean; overload;
var
  Slots, OtherSlots: TIntegerArray;
  Slot, OtherSlot: Int32;
begin
  Self.SetSelectedSlot(-1); // Item images change when selected...

  Result := Self.FindItem(Item, Slots) and Self.FindItem(OtherItem, OtherSlots);
  if Result then
  begin
    Slot := Slots[Random(Length(Slots))];
    OtherSlot := Self.RandomSlotNearby(Slot, OtherSlots);
  
    Result := Self.Use(Slot, OtherSlot);
  end;
end;

(*
Inventory.ShiftDrop
~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.ShiftDrop(Items: TStringArray; Pattern: TIntegerArray): Boolean;

Shift drops items following the desired pattern.

Example
-------

  // Shift drop maple & willow logs in the snake pattern
  Inventory.ShiftDrop(['Maple logs', 'Willow logs'], DROP_PATTERN_SNAKE);
*)
function TRSInventory.ShiftDrop(items: TRSItemArray; pattern: TIntegerArray): Boolean; overload;
var
  slots, order: TIntegerArray;
  i: Int32;
begin
  if Self.FindItems(items, slots) then
  begin
    for i := 0 to High(pattern) do
      if slots.Find(pattern[i]) > -1 then
        order += pattern[i];
  
    Result := Self.ShiftDrop(order);
  end;
end;

var
  Inventory: TRSInventory;

procedure TRSClient.ClientModeChanged; override;
begin
  inherited;

  Inventory.SetupAlignment(Self.Mode);
end;

procedure TSRL.Setup; override;
begin
  Inventory.Setup();

  inherited;
end;

procedure TSRL.Debug(Bitmap: TMufasaBitmap); override;
begin
  inherited;

  Inventory.Draw(Bitmap);
end;
