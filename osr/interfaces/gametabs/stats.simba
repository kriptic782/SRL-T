{$DEFINE SRL_STATS_INCLUDED}
{$IFNDEF SRL_OSR}
  {$I SRL-T/osr.simba}
{$ENDIF}

type
  ERSSkill = (
    ATTACK,
    HITPOINTS,
    MINING,
    STRENGTH,
    AGILITY,
    SMITHING,
    DEFENCE,
    HERBLORE,
    FISHING,
    RANGE,
    THIEVING,
    COOKING,
    PRAYER,
    CRAFTING,
    FIREMAKING,
    MAGIC,
    FLETCHING,
    WOODCUTTING,
    RUNECRAFTING,
    SLAYER,
    FARMING,
    CONSTRUCTION,
    HUNTER,
    TOTAL
  );

const
  SKILLNAMES = [
   'Attack',
   'Hitpoints',
   'Mining',
   'Strength',
   'Agility',
   'Smithing',
   'Defence',
   'Herblore',
   'Fishing',
   'Range',
   'Thieving',
   'Cooking',
   'Prayer',
   'Crafting',
   'Firemaking',
   'Magic',
   'Fletching',
   'Woodcutting',
   'Runecraft',
   'Slayer',
   'Farming',
   'Construction',
   'Hunter',
   'Total'
  ];

type
  TRSSkillInfo = record
    Level:     Int32;
    XP:        Int32;
    Next:      Int32;
    Remainder: Int32;
  end;

type
  TRSStats = record(TRSInterface)
    Levels: array [ERSSkill] of Int32;
  end;

(*
Stats.Setup
~~~~~~~~~~~
.. pascal:: procedure TRSStats.Setup(); override;

Internal method automatically called by SRL.Setup()
*)
procedure TRSStats.Setup(); override;
begin
  inherited;

  Self.Name := 'Stats';
end;

(*
Stats.SetupAlignment
~~~~~~~~~~~~~~~~~~~~
.. pascal:: procedure TRSStats.SetupAlignment(Mode: ERSClientMode); override;

Internal method automatically called by RSClient.ClientModeChanged()
*)
procedure TRSStats.SetupAlignment(Mode: ERSClientMode); override;
begin
  inherited;

  Self.Alignment.Left := [@GameTab.X1];
  Self.Alignment.Right := [@GameTab.X2];
  Self.Alignment.Top := [@GameTab.Y1];
  Self.Alignment.Bottom := [@GameTab.Y2];
end;

(*
Stats.GetSkillBoxes
~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSStats.GetSkillBoxes(): TBoxArray;

Internal method used to retrieve the skill boxes.

Example
-------

  Debug(Stats.GetSkillBoxes());
*)
function TRSStats.GetSkillBoxes(): TBoxArray;
begin
  Result := Grid(3, 8, 53, 23, [10, 9], [Self.X1 + 2, Self.Y1 + 6]);
end;

(*
Stats.GetSkillBox
~~~~~~~~~~~~~~~~~
.. pascal:: function TRSStats.GetSkillBox(skill: ERSSkill): TBox;

Method used to retrieve a skill box.

Example
-------

  Debug(Stats.GetSkillBox(ERSSkill.AGILITY));
*)
function TRSStats.GetSkillBox(skill: ERSSkill): TBox;
begin
  Result := Self.GetSkillBoxes()[Ord(skill)];
end;

(*
Stats.IsOpen
~~~~~~~~~~~~
.. pascal:: function TRSStats.IsOpen(): Boolean;

Returns true/false if the gametab is open or not.

Example
-------

  WriteLn Stats.IsOpen();
*)
function TRSStats.IsOpen(): Boolean;
begin
  Result := (GameTabs.GetCurrentTab() = ERSGameTab.STATS);
end;

(*
Stats.Open
~~~~~~~~~~
.. pascal:: function TRSStats.Open(): Boolean;

Attempts to open the gametab.

Example
-------

  WriteLn Stats.Open();
*)
function TRSStats.Open(): Boolean;
begin
  Result := GameTabs.Open(ERSGameTab.STATS);
end;

(*
Stats.MouseOver
~~~~~~~~~~~~~~~
.. pascal:: function TRSStats.MouseOver(skill: ERSSkill): Boolean;

Moves the mouse over to a skill box.

Example
-------

  WriteLn Stats.MouseOver(ERSSkill.CONSTRUCTION);
*)
function TRSStats.MouseOver(skill: ERSSkill): Boolean;
begin
  Result := Self.Open();
  if Result then
    Mouse.Move(Self.GetSkillBox(skill));
end;

(*
Stats.GetSkillInfo
~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSStats.GetSkillInfo(skill: ERSSkill; waitTime: Int32 = 1000): TRSSkillInfo;

Get the skill information about a skill when you hover it.

Example
-------

  WriteLn Stats.GetSkillInfo(ERSSkill.CONSTRUCTION);
*)
function TRSStats.GetSkillInfo(skill: ERSSkill; waitTime: Int32 = 1000): TRSSkillInfo;
var
  TPA: TPointArray;
  Lines: TStringArray;
  T: UInt64;
  B: TBox;
  I: Int32;
begin
  if not Self.MouseOver(skill) then
    Exit();

  T := GetTickCount() + waitTime;
  repeat
    if SRL.FindColors(TPA, CTS0($A0FFFF), Self.Bounds) then
    begin
      B := TPA.Bounds;

      SetLength(Lines, B.Height div 12);
      for I := 0 to High(Lines) do
        Lines[I] := OCR.Recognize([B.X1, B.Y1 + (I * 12), B.X2, B.Y1 + ((I+1) * 12) + 3],  TOCRColorFilter.Create([0]), RS_FONT_PLAIN_12);

      if Length(Lines) = 0 then
        Continue;
      if not (SKILLNAMES[Skill] in Lines[0]) then
        Continue;

      Result.XP := StrToIntDef(ExtractFromStr(Lines[0], Numbers), 0);
      Result.Level := SRL.GetLevelAtXP(Result.XP);

      if Length(Lines) = 3 then
      begin
        Result.Next := StrToIntDef(ExtractFromStr(Lines[1], Numbers), 0);
        Result.Remainder := StrToIntDef(ExtractFromStr(Lines[2], Numbers), 0);
      end;

      Break;
    end;

    Wait(50);
  until GetTickCount() >= T;
end;

(*
Stats.GetCurrentLevel
~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSStats.GetCurrentLevel(skill: ERSSkill): Int32; deprecated 'Use Stats.GetLevel() instead.'

Get the level of the specified **skill**.

.. note:: This method is deprecated. Use Stats.GetLevel() instead.

Example
-------

  WriteLn Stats.GetCurrentLevel(ERSSkill.HITPOINTS);
*)
function TRSStats.GetCurrentLevel(skill: ERSSkill): Int32; deprecated 'Use Stats.GetLevel() instead.'
var
  text: String;
  b: TBox;
begin
  if not Self.Open() then
    Exit(-1);

  b := Self.GetSkillBox(skill);
  b.Y2 := b.Y1 + (b.Height() div 2);

  text := OCR.Recognize(b, TOCRColorFilter.Create([65535]), RS_FONT_PLAIN_11);
  Result := StrToIntDef(ExtractFromStr(text, Numbers), -1);
end;

(*
Stats.GetLevel
~~~~~~~~~~~~~~
.. pascal:: function TRSStats.GetLevel(skill: ERSSkill; useCache: Boolean = True): Int32;

Get the level of the specified **skill**. By default uses the levels cached in the Stats.Levels array.
When using the cached methods, it can be used to retrieve the level of a skill while the gametab is open assuming the level is already cached.

Example
-------

  WriteLn Stats.GetLevel(ERSSkill.HITPOINTS);
*)
function TRSStats.GetLevel(skill: ERSSkill; useCache: Boolean = True): Int32;
var
  text: String;
  b: TBox;
begin
  if useCache then
  begin
    Result := Self.Levels[skill];
    if Result > 0 then
      Exit;
  end;

  if not Self.Open() then
    Exit(-1);

  b := Self.GetSkillBox(skill);
  b.Y2 := b.Y1 + (b.Height() div 2);

  text := OCR.Recognize(b, TOCRColorFilter.Create([65535]), RS_FONT_PLAIN_11);
  Result := StrToIntDef(ExtractFromStr(text, Numbers), -1);

  if Result > 0 then
    Self.Levels[skill] := Result;
end;

(*
Stats.CacheAll
~~~~~~~~~~~~~~
.. pascal:: procedure TRSStats.CacheAll();

Method to update all cached levels.
*)
procedure TRSStats.CacheAll();
var
  i: Int32;
begin
  for i := 0 to Ord(High(ERSSkill)) do
    Self.GetLevel(ERSSkill(i), False);
end;

(*
Stats.IncrementCachedLevel
~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: procedure TRSStats.IncrementCachedLevel(skill: ERSSkill);

Internal method to update cached levels. This is automatically called by Chat.LeveledUp().

Example
-------

  WriteLn Stats.GetCurrentLevel(ERSSkill.PRAYER);
  Stats.IncrementLevel(ERSSkill.PRAYER);
  WriteLn Stats.GetCurrentLevel(ERSSkill.PRAYER);
*)
procedure TRSStats.IncrementCachedLevel(skill: ERSSkill);
begin
  if Self.Levels[skill] > 0 then
    Self.Levels[skill] += 1;
end;


procedure TRSStats.Draw(Bitmap: TMufasaBitmap); override;
begin
  if not Self.IsOpen() then
    Exit;

  inherited();

  Bitmap.DrawBoxes(Self.GetSkillBoxes(), $00FFFF);
end;

var
  Stats: TRSStats;

procedure TRSClient.ClientModeChanged(); override;
begin
  inherited;

  Stats.SetupAlignment(Self.Mode);
end;

procedure TSRL.Setup(); override;
begin
  inherited;

  Stats.Setup();
end;

procedure TSRL.Debug(Bitmap: TMufasaBitmap); override;
begin
  inherited;

  Stats.Draw(Bitmap);
end;

(*
Chat.LeveledUp
~~~~~~~~~~~~~~
.. pascal:: function TRSChat.LeveledUp(): Boolean;

Returns true if we have the level up message on the chat box and attempts
to update Stats.Levels

Example
-------

  if Chat.LeveledUp then
    Chat.ClickContinue(True);
*)
function TRSChat.LeveledUp(): Boolean;
var
  b: TBox;
  i: Int32;
  skillStr: String;
begin
  Result := Self.FindOption('Congratulations', [CHAT_COLOR_NAVY]);
  if not Result then
    Exit;

  b := Self.Bounds();
  b.Y1 += 60;
  b.Y2 -= 75;

  skillStr := OCR.Recognize(b, TOCRColorFilter.Create([0]), RS_FONT_QUILL_8);
  skillStr := Trim(skillStr.After('Your ').Before(' level').Lower());

  for i := 0 to Ord(High(ERSSkill)) do
    if skillStr in ToStr(ERSSkill(i)).After('ERSSkill.').Lower() then
      Break;

  if i >= Ord(High(ERSSkill)) then //total never has level ups...
    Exit;

  Stats.IncrementCachedLevel(ERSSkill(i));
end;
